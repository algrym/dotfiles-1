#!/usr/bin/env perl

use strict;
use warnings;
use JSON;

# Donâ€™t buffer any output.
$| = 1;

open(my $i3status, "-|", "i3status") or die $!;

# Skip the first two lines which contains the version header and the start of
# an infinite array.
print(scalar <$i3status>);
print(scalar <$i3status>);

# Read lines forever, ignore a comma at the beginning if it exists.
while (my ($statusline) = (<$i3status> =~ /^,?(.*)/)) {
  my @blocks = @{decode_json($statusline)};

  # Music playing:
  if (`pgrep deadbeef`) {
    my $music = `deadbeef --nowplaying-tf '%artist% - %title%'`;
    splice(@blocks, 1, 0, {
      full_text => "\x{f025} $music",
      color => '#D78787'
    });
  }

  # Backlight
  splice(@blocks, -1, 0, {
    full_text => sprintf("\x{f0eb} %.0f", `xbacklight`)
  });

  # Keyboard layout
  my ($keyboard_layout) = `setxkbmap -query` =~ /layout:\s+(.*?)$/;
  splice(@blocks, -1, 0, {
    full_text => sprintf("\x{f11c} %s", uc $keyboard_layout)
  });

  print encode_json(\@blocks) . ",\n";
}

close($i3status);
