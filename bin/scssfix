#!/usr/bin/sed -f
# Sanitize scss files
#
# Usage:
#	scssfix [-i] <file>
#
# Version: 0.2

# Convert every four leading space to tab:
:stab
s/^\(\t*\)    /\1\t/; t stab

# Convert block comments to multiply single line comments:
/^\/\*/ {
	:scom
	/\*\//b ecom
	N
	b scom
	:ecom

	s=/\*==
	s=\*/==
	s=\(^\|\n\)\(// *\)*=\1// \2=g
	b
}

# Convert comment colors, goto end if case of match
s/color: *white *;$/color: #fff;/; t
s/color: *black *;$/color: #000;/; t
s/color: *gr[ae]y;$/color: #080808;/; t

# Add trailing lines between blocks
:sline
/}$/ {
	N
	$b
	//b sline
	s/}\n\([[:space:]]*[@&#.*:[:alnum:]]\)/}\n\n\1/

	# Print and remove everything but the last line from the patter space,
	# effectially leaving us with a "normal" line in the pattern space:
	h
	s/\n[^\n]*$//p
	x
	s/^.*\n//

}

# Remove empty blocks
/{$/ {
	N;
	/{\n[[:space:]]*}/d

	P;D
}

# Lowercase hexidecimal colors
s/#\([[:xdigit:]]\{3\}\)\{1,2\}/\L&/
